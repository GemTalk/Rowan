#!/usr/bin/env superdoit_stone
options
{
SuperDoitOptionalOptionWithRequiredArg long: 'archBase'.
SuperDoitOptionalOptionWithRequiredArg long: 'projectsHome' default: '$ROWAN_PROJECTS_HOME'.
}
%
usage
-----
USAGE 
  $basename [--help | -h] [--debug | -D] [OPTIONS]

DESCRIPTION
  Attach the standard Rowan development projects in the stone to git clones:
    gemstoneBaseImage to $ARCHBASE/image, if ARCHBASE env var defined
    Rowan             to $ROWAN_PROJECTS_HOME/Rowan
    RowanClientServices to $ROWAN_PROJECTS_HOME/RowanClientServices
    
  and reload Rowan, including tests.

OPTIONS
  -h, --help                 display usage message
  -D, --debug                bring up topaz debugger in the event of a script error

EXAMPLES
  $basename -h
  $basename --help -- -L -I .topazini
  $basename --projectsHome=$GS_HOME/shared/repos 
  $basename --projectsHome=/home/dhenrich/work/m_37x_externals_st --archBase=/home/dhenrich/work/m_37x
-----
%
method
attachRowanProjects: projectsHome
	| rowanProject loadSpecs projectNames |
	rowanProject := Rowan projectNamed: 'Rowan'.
	loadSpecs := rowanProject loadedLoadSpecifications.
	{
		'Announcements' .
		'FileSystemGs' .
		'RemoteServiceReplication' .
		'RowanClientServices' .
	}
		do: [:projectName |
			"pick up the extra rowan3 projects"
			(Rowan projectNamed: projectName ifAbsent: [])
				ifNotNil: [:project |
					| loadSpec |
					loadSpec :=  project loadSpecification.
					loadSpecs addLoadSpec: loadSpec ] ].
	loadSpecs do: [:loadSpec |
		| relRoot |
		relRoot := loadSpec relativeRepositoryRoot.
		relRoot isEmpty
			ifTrue: [ 
				loadSpec 
					projectsHome: projectsHome;
					gitUrl: 'file:', ((projectsHome, '/', loadSpec projectAlias) asFileReference pathString);
					yourself  ]
			ifFalse: [
						"embedded Rowan project" 
						loadSpec
							gitUrl: 'file:', (((projectsHome, '/', loadSpec projectAlias) asFileReference / relRoot) pathString);
							projectsHome: projectsHome;
							yourself ].
		loadSpec addCustomConditionalAttributes: #('tests' 'testsv2' 'obsolete') ].
 	projectNames := loadSpecs load projectNames asArray sort. 
	self logMessage: 'Rowan projects attached and reloaded: '.
	projectNames do: [:each |
		self logMessage: '	', each ]
%
doit
	| rowanProject loadSpecs gitUrl projectsHome |
	(Rowan version >= (RwSemanticVersionNumber fromString: '3.0.0'))
		ifFalse: [ self error: 'This script should be run in Rowan v3.0 or later'].
	self projectsHome
		ifNotNil: [:value |
			projectsHome := value asFileReference pathString.
			System gemEnvironmentVariable: 'ROWAN_PROJECTS_HOME' put: projectsHome ]
		ifNil: [ 
			(System gemEnvironmentVariable: 'ROWAN_PROJECTS_HOME')
				ifNotNil: [:value | projectsHome := value asFileReference pathString ]
				ifNil: [ self error: 'The env var $ROWAN_PROJECTS_HOME is expected to be defined, or the --projectsHome option used.' ] ].
	self logMessage: 'Using ROWAN_PROJECTS_HOME = ', '$ROWAN_PROJECTS_HOME' asFileReference pathString.
	self logMessage: 'Rowan version: ', Rowan versionString.
	(self archBase
		ifNil: [ System gemEnvironmentVariable: 'ARCHBASE' ]
		ifNotNil: [:value | value ])
		ifNil: [ self logMessage: 'ARCHBASE not set, so gemstoneBaseImage project not attached and not loaded']
		ifNotNil: [:value |
			"attach gemstoneBaseImage project"
			| loadSpec |
			System gemEnvironmentVariable: 'ARCHBASE' put: value asFileReference pathString.
			self logMessage: 'Using ARCHBASE = ', '$ARCHBASE' asFileReference pathString.
		 	loadSpec := (Rowan projectNamed: 'gemstoneBaseImage') loadSpecification.
			gitUrl := loadSpec gitUrl.
			(gitUrl notNil and: [gitUrl beginsWith: 'file:'])
					ifTrue: [ 
						"project already attached, so reload"
						loadSpec
							addCustomConditionalAttributes: #('tests');
							load.
						self logMessage: 'gemstoneBaseImage reloaded' ] 
					ifFalse: [
						loadSpec
							gitUrl: 'file:', ('$ARCHBASE' asFileReference pathString);
							projectSpecFile: 'image/rowan/project_attached.ston';
							addCustomConditionalAttributes: #('tests');
							load.
						self logMessage: 'gemstoneBaseImage project attached and reloaded' ] ].
	self attachRowanProjects: projectsHome.
	^ System commit
%
